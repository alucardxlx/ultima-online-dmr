//-----------------------------------------------------------------------------

use npc;

//-----------------------------------------------------------------------------

include ":halloween:halloween";
include "include/cmds/misc";
include "ai/include/ainpcs";
include "ai/include/npch";
include "ai/include/npcspeech";
include "ai/setup/citynpcsetup";
include "ai/include/smartrun";
include "include/items";


//-----------------------------------------------------------------------------

EnableEvents( EVID_SPEECH,SPEECH_RANGE);
EnableEvents( EVID_ITEM_GIVEN );

//-----------------------------------------------------------------------------

program RoyalMerchant_AI()

  SetLogRunCycles(LST_AI);

  while (me)

    if(!ev)
      ev := wait_for_event( waittime );
    endif

    StdCityNpcHandleEvent(ev);
    StdCityNpcEventLoop();
    
  endwhile

endprogram

//-----------------------------------------------------------------------------

function CheckMySpeech(who, text)

  if(text["kupi"])
    TurnToward(who);
    if(CanNpcSpeakWith(who))
        StartTrade(who);
    endif
  else
    return 0;
  endif

  return 1;

endfunction

//-----------------------------------------------------------------------------

function StartTrade(who)

  var Items := array;
  GetHalloweenDonationItems(Items);
  var Res := struct;

  Res.+type    := EVID_NH_HALLOWEEN_GUMP;
  Res.+source  := who;
  Res.+npc     := me;
  Res.+items   := Items;
  StdNpcHandleWait(Res);
  if(ev == error)
    return;
  endif
  
  if(!CanIHearPlayer(who) or !CanNpcSpeakWith(who))
    SendSysMessage(who, "Jestes za daleko od sprzedawcy", FONT_NORMAL, COLOR_RED);
    return;
  endif

  if(Res.cancel)
    say("Zapraszam wiec innym razem");
    return;
  
  elseif(Res.selitem)
    var objDesc := GetItemDescriptor(Res.selitem);
    var price;
    foreach Item in Items
      if(Cint(Item[1])==Cint(Res.selitem) || Cint(Item[3])==Cint(Res.selitem))
        price := Cint(Item[2]);
      endif
    endforeach
    if(price)
      var coins := CInt(GetObjProperty(who, "HalloweenPoints"));
      if(coins<price)
        say("Niestety nie stac cie na ten przedmiot");
		Say("Masz tylko " + coins + " a potrzebujesz na ten przedmiot " + price);
        return;
      endif
      SetObjProperty(who, "HalloweenPoints", coins - price);
      var Item := CreateItemInPack(who, objDesc);
      if(Item)
        Say("Prosze bardzo i zapraszam ponownie!");
        SetItemBound(Item, who);
        var time := ReadConfigFile(ITEMS_CFG)[Item.objtype].TimeExist;
          if(time)
          SetObjProperty(Item, "available", ReadGameClock()+time);
        endif
      else
        Say("Nie moge Ci teraz tego sprzedac.");
      endif
    endif
  endif
endfunction

//--------------------------------------------------------------------
