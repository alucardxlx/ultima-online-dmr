use uo;
use polsys;

include "include/clock";
include "include/gumps";
include ":major:major";

var Res := 1;
var Cities := GetCities();

program VoteStonesStatus(who)

    BuildMainMenu();
    while(IsLoggedIn(who) and Res)
        Res := GSend(who)[0];
        BuildMainMenu();
        if(Res > 20 and Res < 60)
            ShowStone(Res-20);
            continue;
        elseif(Res > 60 and Res < 90)
            MoveToStone(who,Res-60);
        endif
    endwhile

endprogram

function BuildMainMenu()

    GInit();
    GResPic(100,100,5120,400,420);
    GTextLine(150,110,63,"Status Kamieni           Burmistrz");

    var i := 1,Stone,Major;
    foreach City in Cities
        Stone := FindVStone(City);
        Major := GetMajor(Stone);
        GTextLine(110,140+(i*20),63,City);
        if(Stone)
            GTextLine(250,140+(i*20),500,"EX");
            GButton(280,143+(i*20),2117,2118,20+i);
        else
            GTextLine(250,140+(i*20),500,"NA");
        endif
        if(Major)
            GTextLine(330,140+(i*20),650,Major.name);
        else
            GTextLine(330,140+(i*20),650,"NA");
        endif
        i := i + 1;
    endforeach

endfunction
 
function ShowStone(CityNr)

    var City    := Cities[CityNr];
    var Stone   := FindVStone(City);
    var Major   := GetMajor(Stone);

    GInit();
    GResPic(100,100,5120,400,400);
    GTextLine(200,110,63,"Status Kamienia miasta "+City);
    GTextLine(110,140,500,"x: "+Stone.x+" y: "+Stone.y+" z: "+Stone.z);

    case(GetVoteStatus(Stone))
        VOTE_START:  GTextLine(110,170,500,"Rozpoczete");
                     GTextLine(110,200,500,RealTime(GetStoneTime(Stone) + VOTE_TIME - ReadGameClock()));
        VOTE_END:    GTextLine(110,170,500,"Zakonczone");
                     GTextLine(110,200,500,RealTime(GetStoneTime(Stone) + MAJOR_TIME - ReadGameClock()));
                     GTextLine(110,230,500,Major.name);
        VOTE_STOP:   GTextLine(110,170,500,"Zatrzymane");
                     GTextLine(110,200,500,RealTime(GetStoneTime(Stone) + BREAK_TIME - ReadGameClock()));
    endcase

    GButton(400,140,2117,2118,60+CityNr);

endfunction

function MoveToStone(who, Nr)

    var City    := Cities[Nr];
    var Stone   := FindVStone(City);

    SendSysMessage(who, "Przenoszenie do kamienia miasta "+City+".",FONT_NORMAL,GREEN_COLOR);

    MoveObjectToLocation(who, Stone.x, Stone.y, Stone.z, 1, Stone.realm);

endfunction
