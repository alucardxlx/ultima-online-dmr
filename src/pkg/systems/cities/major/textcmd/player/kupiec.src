use basic;
use uo;

include "include/findcity";
include "include/coords";
include ":major:major";
include "include/gumps";
include "include/npcs";
include "include/player/misc";

var Stone,City,i,Vendor;

program MajorVendors(who,text)

  City  := IsMajor(who);
  Stone := FindVStone(City);
  
  if(!City)
    return 0;
  endif

  case(text)
    "dodaj":   AddMerchant(who);
    "kasuj":   EraseMerchant(who);
    "status":  Status(who);
    default:   SendSysMessage(who,".kupiec [dodaj/kasuj/status]",FONT_NORMAL,COLOR_BLUE);
  endcase

endprogram

function AddMerchant(who)

  if(Stone)
    var Vendors := GetMajorVendors(Stone);
    if(len(Vendors) >= MAX_VENDORS)
      SendSysMessage(who,"Nie mozesz miec wiecej kupcow!",FONT_NORMAL,COLOR_RED);
      return;
    endif

    SendSysMessage(who,"Gdzie chcesz by kupiec stal?",FONT_NORMAL,COLOR_GREEN);
    var Place := TargetCoordinates(who);
    if(!Place)
      SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
      return;
    endif

    if(coordist(place.x,place.y,who.x,who.y) > 3)
      SendSysMessage(who,"Wybrane miejsce jest za daleko.",FONT_NORMAL,COLOR_RED);
      return;
    endif

    if(IsMajor(who) != FindCityCoords(Place.x,Place.y, Place.Realm))
      SendSysMessage(who,"Nie jestes na terytorium swojego miasta.",FONT_NORMAL,COLOR_RED);
      return;
    endif
    
    AddMajorVendor(Stone,Place,who);
  endif
  return 0;

endfunction

function EraseMerchant(who)

  var Vendors := GetMajorVendors(Stone);

  GInit(100,100);
  GResPic(0,0,5120,230,200);
  GTextLine(30,10,63,"Wybierz kupca : ");

  if(!Vendors or !Vendors.size())
    SendSysMessage(who,"Nie masz zadnych kupcow!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  for(i:=1;i<=len(Vendors);i:=i+1)
    Vendor := SystemFindObjectBySerial(CInt(Vendors[i]));
    GButton(10,CInt(23+(i*20)),2117,2118,i);
    GTextLine(30,CInt(20+(i*20)),500,CLStr(Vendor.name));
  endfor

  GButton(80,140,1144,1145,100);

  var res := GSend(who);

  if(res[0] > 0 and res[0] <= len(Vendors))
    DismissVendor(Stone,res[0]);
  else
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
  endif

endfunction

function Status(who)

  var Vendors := GetMajorVendors(Stone);

  GInit(100,100);
  GResPic(0,0,5120,230,200);
  GTextLine(80,10,63,"Kupcy : ");

  if(!Vendors.size())
    SendSysMessage(who,"Nie masz zadnych kupcow!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  for(i:=1;i<=len(Vendors);i:=i+1)
    Vendor := SystemFindObjectBySerial(CInt(Vendors[i]));
    GTextLine(10,CInt(20+(i*20)),500,CLStr(Vendor.name));
  endfor

  GButton(80,140,1147,1148,100);
  GSend(who);

endfunction
