use basic;
use polsys;
use uo;
include "major";
include "include/gumps";
include "include/gumpboxes";
include ":character:checks";
include "include/tempmods";

var Res := 1,i,Candidates;
var City,Major;

program VoteStone(who,Stone)

  City := GetCity(Stone);
  BuildMainMenu(who,Stone);

  while(IsLoggedIn(who) and Stone and Res)
    Res := GSend(who);

    if(!Res[0])
      return;
    endif

    case(Res[0])
      2:  ChangeVoteStatus(Stone,VOTE_STOP);
          BroadCast("Glosowanie na burmistrza miasta "+GetCity(Stone)+" zatrzymane!",FONT_NORMAL,COLOR_RED);
      3:  ChangeVoteStatus(Stone,VOTE_STOP);
      4:  ChangeVoteStatus(Stone,VOTE_START);
          BroadCast("Glosowanie na burmistrza miasta "+GetCity(Stone)+" rozpoczete!",FONT_NORMAL,COLOR_BLUE);      
      10: if(Candidate(who,Stone))
            continue;
          endif
      11: Vote(who,Stone);
          continue;
      12: ShowVotes(who,Stone);
          continue;
      13: AddCandidate(who,Stone,Res);
      14: DismissVote(Stone,who);
      15: ShowGuards(who,Stone);
          continue;
      16: AddGuard(who,Stone);
    endcase

    if(Res[0] > 300 and Res[0] <= 300 + MAX_GUARDS)
      DeleteGuard(who,Stone,Res[0]-300);
    elseif(Res[0] > 200 and Res[0] <= 205)
      ShowDesc(Stone,Res[0]-200);
      continue;
    elseif(Res[0] > 100 and Res[0] <= 105)
      VoteOn(who,Stone,Res[0]-100);
    endif
    
    BuildMainMenu(who,Stone);
  endwhile

endprogram

function MainGump(Stone)

  GInit(80,80);
  GResPic(0,0,3600,400,300);
  GTextLine(30,20,560,Stone.name);

endfunction

function BuildMainMenu(who,Stone)

  MainGump(Stone);
  Major := 0;

  case(GetVoteStatus(Stone))
    VOTE_STOP:  GTextLine(40,50,550,"Glosowanie Zatrzymane");
                if(IsAdmin(who))
                  GButton(20,230,2714,2715,4);
                  GTextLine(40,230,540,"Rozpocznij glosowanie");
                endif
                GTextLine(40,260,550,"Czas do rozpoczecia : "+GameTime(GetStoneTime(Stone) + BREAK_TIME - ReadGameClock()));

    VOTE_START: GTextLine(40,50,550,"Glosowanie Rozpoczete");
                GButton(20,100,2714,2715,10);
                GTextLine(40,100,540,"Kandyduj");
                GButton(20,130,2714,2715,11);
                GTextLine(40,130,540,"Glosuj");
                GButton(20,160,2714,2715,12);
                GTextLine(40,160,540,"Wyniki");
                if(IsAdmin(who))
                  GButton(20,230,2714,2715,2);
                  GTextLine(40,230,540,"Zatrzymaj glosowanie");
                endif
                GTextLine(40,260,550,"Czas do konca : "+GameTime(GetStoneTime(Stone) + VOTE_TIME - ReadGameClock()));
                
    VOTE_END:   Major := GetMajor(City);
                GTextLine(40,50,550,"Glosowanie Zakonczone");
                GButton(265,50,2714,2715,15);
                GTextLine(290,50,550,"Straznicy");
                GTextLine(40,260,550,"Czas do konca : "+GameTime(GetStoneTime(Stone) + MAJOR_TIME - ReadGameClock()));
                GTextLine(50,100,540,"Burmistrz :");
                GTextLine(140,100,550,ClStr(Major.name));
                GTextLine(50,160,570,"Wygral"+ggm(Major,4)+" "+CInt(GetObjProperty(Stone,"WinVotes"))+" glosami.");
                GTextLine(50,190,570,CInt(GetObjProperty(Stone,"Votes"))+" osob chce "+ggm(Major,16)+" odwolac.");
                GButton(25,220,2714,2715,14);
                GTextLine(50,220,570,"Glosuj na odwolanie");
                if(IsAdmin(who))
                  GButton(220,230,2714,2715,3);
                  GTextLine(240,230,540,"Odwolaj burmistrza");
                endif
  endcase

endfunction

function Candidate(who,Stone)

  if(GetVoteStatus(Stone) != VOTE_START)
    return;
  endif

  Candidates := GetCandidates(Stone);

  if(!CanVote(who,Stone))
    return 0;
  endif

  if(GetRealAge(who) < NEEDED_CAND_AGE)
    SendSysMessage(who,"Jestes za mlody by moc kandydowac na burmistrza.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif

  if(GetCitizenData(who).signtime + NEEDED_CITIZEN_TIME > ReadGameClock())
    if(!GetObjProperty(who, "CityException"))
		SendSysMessage(who,"Jestes za krotko mieszkancem tego miasta!",FONT_NORMAL,COLOR_RED);
		return 0;
	endif
  endif
  
  if(Candidates.size() >= MAX_CANDIDATES)
    SendSysMessage(who,"Nie mozesz juz kandydowac.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif


  MainGump(Stone);

  GResPic(30,60,BKGND_GOLDTRIM,340,185);
  GTextEntry(42,70,315,20,570,10, "");
  GTextEntry(42,90,315,20,570,11, "");
  GTextEntry(42,110,315,20,570,12, "");
  GTextEntry(42,130,315,20,570,13, "");
  GTextEntry(42,150,315,20,570,14, "");
  GTextEntry(42,170,315,20,570,15, "");
  GTextEntry(42,190,315,20,570,16, "");
  GTextEntry(42,210,315,20,570,17, "");


  GButton(20,260,2714,2715,9999);
  GTextLine(40,260,550,"Wroc");
  
  GButton(290,260,2714,2715,13);
  GTextLine(310,260,550,"Kandyduj");

  return 1;

endfunction

function Vote(who,Stone)

  if(GetVoteStatus(Stone) != VOTE_START)
    return;
  endif

  Candidates := GetCandidates(Stone);

  if(!CanVote(who,Stone))
    return 0;
  endif

  var VotingPers := GetVotingPersons(Stone);

  if(who.acctname in VotingPers)
    SendSysMessage(who,"Juz glosowal"+ggm(who,3)+" na burmistrza w tym miescie.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  
  MainGump(Stone);

  i := 1;

  GResPic(30,60,BKGND_GOLDTRIM,340,185);

  GTextLine(62 ,75,560,"Kandydat");
  GTextLine(232,75,560,"Glosy");
  GTextLine(292,75,560,"Glos Opis");

  foreach Cand in Candidates
    GTextLine(42,80+(i*25),540,i);
    GTextLine(62,80+(i*25),500,Cand.name);
    GTextLine(242,80+(i*25),570,Cand.votes);
    GButton(300,80+(i*25),2714,2715,100+i);
    GButton(335,80+(i*25),2714,2715,200+i);
    i := i + 1;
  endforeach

  GButton(20,260,2714,2715,9999);
  GTextLine(40,260,550,"Wroc");

endfunction

function ShowVotes(who,Stone)

  if(GetVoteStatus(Stone) != VOTE_START)
    return;
  endif

  Candidates := GetCandidates(Stone);

  MainGump(Stone);

  i := 1;

  GResPic(30,60,BKGND_GOLDTRIM,340,185);

  GTextLine(62 ,75,560,"Kandydat");
  GTextLine(232,75,560,"Glosy");
  GTextLine(327,75,560,"Opis");

  foreach Cand in Candidates
    GTextLine(42,80+(i*25),540,i);
    GTextLine(62,80+(i*25),500,Cand.name);
    GTextLine(242,80+(i*25),570,Cand.votes);
    GButton(335,80+(i*25),2714,2715,200+i);
    i := i + 1;
  endforeach

  GButton(20,260,2714,2715,9999);
  GTextLine(40,260,550,"Wroc");

endfunction

function ShowDesc(Stone,Nr)

  if(GetVoteStatus(Stone) != VOTE_START)
    return;
  endif

  Candidates := GetCandidates(Stone);

  var Desc := Candidates[Nr].Desc;

  GInit(80,80);
  GResPic(0,0,3600,400,300);
  GTextLine(30,20,560,"Kandydat "+Candidates[Nr].name);

  GResPic(30,60,BKGND_GOLDTRIM,340,185);
  GTextCrop(42,70,315,20,570,ClStr(Desc[1]));
  GTextCrop(42,90,315,20,570,ClStr(Desc[2]));
  GTextCrop(42,110,315,20,570,ClStr(Desc[3]));
  GTextCrop(42,130,315,20,570,ClStr(Desc[4]));
  GTextCrop(42,150,315,20,570,ClStr(Desc[5]));
  GTextCrop(42,170,315,20,570,ClStr(Desc[6]));
  GTextCrop(42,190,315,20,570,ClStr(Desc[7]));
  GTextCrop(42,210,315,20,570,ClStr(Desc[8]));

  GButton(20,260,2714,2715,9999);
  GTextLine(40,260,550,"Wroc");

endfunction

function VoteOn(who,Stone,Nr)

  if(GetVoteStatus(Stone) != VOTE_START)
    return;
  endif

  Candidates := GetCandidates(Stone);
  SendSysMessage(who,"Zaglosowal"+ggm(who,3)+" na "+Candidates[Nr].name+".",FONT_NORMAL,COLOR_BLUE);
  Candidates[Nr].votes := Candidates[Nr].votes + 1;
  AddVotingPerson(Stone,who);
  SetCandidates(Stone,Candidates);

endfunction

function DismissVote(Stone,who)

  if(GetVoteStatus(Stone) != VOTE_END)
    return;
  endif

  if(!CanVote(who,Stone))
    return 0;
  endif

  var VotingPers := GetVotingPersons(Stone);

  if(who.acctname in VotingPers)
    SendSysMessage(who,"Juz glosowal"+ggm(who,3)+" na burmistrza w tym miescie.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  
  if(!YesNo(who,"Chcesz glosowac za odwolaniem burmistrza miasta "+City+"?"))
    return;
  endif

  SetObjProperty(Stone,"Votes",CInt(GetObjProperty(Stone,"Votes")) + 1);
  SendSysMessage(who,"Zaglosowal"+ggm(who,3)+" na odwolanie burmistrza miasta "+City+".",FONT_NORMAL,GREEN_COLOR);
  AddVotingPerson(Stone,who);

endfunction

function ShowGuards(who,Stone)

  if(GetVoteStatus(Stone) != VOTE_END)
    return;
  endif

  Candidates := GetCandidates(Stone);
  var Guards := GetMajorGuards(Stone);

  MainGump(Stone);

  i := 1;

  GResPic(30,60,BKGND_GOLDTRIM,340,185);

  GTextLine(62 ,75,560,"Straznicy");
  if(Major == who)
    GTextLine(312,75,560,"Kasuj");
  endif

  foreach Guard in Guards
    Guard := FindPlayerBySerial(Guard);
    GTextLine(42,80+(i*25),540,i);
    GTextLine(62,80+(i*25),500,Guard.name);
    if(Major == who)
      GButton(325,80+(i*25),2714,2715,300+i);
    endif
    i := i + 1;
  endforeach

  GButton(20,260,2714,2715,9999);
  GTextLine(40,260,550,"Wroc");

  if(Major == who)
    GButton(250,260,2714,2715,16);
    GTextLine(270,260,550,"Dodaj straznika");
  endif
  
endfunction

function AddGuard(who,Stone)

  if(GetVoteStatus(Stone) != VOTE_END)
    return;
  endif

  if(GetMajorGuards(Stone).size() >= MAX_GUARDS)
    SendSysMessage(who,"Wiecej nie moze byc straznikow.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  
  SendSysMessage(who,"Wskaz nowego straznika.",FONT_NORMAL,GREEN_COLOR);
  var NewGuard := Target(who);
  
  if(Distance(who,Stone) > 3)
    SendSysMessage(who,"Stoisz za daleko.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(!IsPlayer(NewGuard))
    SendSysMessage(who,"To nie moze byc straznikiem.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(AddMajorGuard(Stone,NewGuard))
    SendSysMessage(who,NewGuard.name + " zostal"+ggm(NewGuard,4)+" nowym straznikiem miasta "+City+".",FONT_NORMAL,GREEN_COLOR);
    SendSysMessage(NewGuard,"Zostal"+ggm(NewGuard,3)+" nowym straznikiem miasta "+City+".",FONT_NORMAL,GREEN_COLOR);
  else
    SendSysMessage(who,"Ta osoba nie moze zostac straznikiem.",FONT_NORMAL,COLOR_RED);
  endif
  
endfunction

function DeleteGuard(who,Stone,Nr)

  if(GetVoteStatus(Stone) != VOTE_END)
    return;
  endif

  var Guard := FindPlayerBySerial(GetMajorGuards(Stone)[Nr]);
  
  if(DismissGuard(Stone,Nr))
    SendSysMessage(who,ClStr(Guard.name)+" nie jest juz straznikiem miasta "+City+".",FONT_NORMAL,GREEN_COLOR);
    SendSysMessage(Guard,"Nie jestes juz straznikiem miasta "+City+".",FONT_NORMAL,COLOR_RED);
  else
    SendSysMessage(who,"Ten straznik nie istnieje.",FONT_NORMAL,COLOR_RED);
  endif
  
endfunction
